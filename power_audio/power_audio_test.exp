#!/usr/bin/expect
#
# Power management test in audio driver
#
# Usage:
#   ./power_audio_test.exp board_type audio_dev pm_state
#
# Example:
#   ./power_audio_test.exp BF609-EZKIT audio_adau1761 standby
#

source ../common/kernel_config.exp

if { $argc < 3} {
    puts "Please input: board_type, audio_dev, pm_state (standby or mem)"
    puts "Refer to build_audio_kernel.exp for audio_dev."
    exit
}
set audio_dev [lindex $argv 1]
set pm_state [lindex $argv 2]
set TITLE [title "$argv0"]
append TITLE " ($audio_dev $pm_state)"
set log_name power_${audio_dev}_${pm_state}_test_log

log_file $log_name
send_user "Starting $argv0\n"
send_user "\n#### Testing audio driver is $audio_dev, power management state is $pm_state. ####\n"


step "Start kermit"
source ../common/spawn_kermit.exp

step "Boot kernel"
# To add following line here to indicate that if the bootargs is different,
# it could be set here. If the bootargs value is 0, it needn't be stated,
# for it is the default value.
# set bootargs  $bootargs_param0
source ../common/kernel_boot.exp

set sleep_time 15
set wakeup_fail 0
set sleep_pattern "Syncing filesystems.*Freezing user space processes \.\.\. \\\(elapsed.*seconds\\\) done\.\r\nFreezing remaining freezable tasks \.\.\. \\\(elapsed.*seconds\\\) done\.\r\nSuspending console\\\(s\\\) \\\(use no_console_suspend to debug\\\)"
set wakeup_pattern "Restarting tasks \.\.\. done"

if { $pm_state == "mem" } {
    if { $board_type == "BF533-STAMP" || $board_type == "BF533-EZKIT" }  {
        set sleep_pattern ""
        set wakeup_pattern "wakeup from.*rtcwake: short write"
    }
}

proc pm_test_case1_reset {} {
    global case_num
    global kernel_prompt
    global wakeup_fail
    global pm_state
    global sleep_pattern
    global wakeup_pattern
    global host_spawn_id
    global kermit_spawn_id
    global spawn_id
    global count_under_run

    set timeout 5
    incr case_num
    send "echo $pm_state > /sys/power/state\r"
    while 1 {
        expect {
            -re $sleep_pattern {
                case_pass $case_num
                break
            }
            timeout {
                send_user "Test case 1: Sleep fail\n"
                case_fail $case_num
            }
        }
    }

    sleep 10

    set spawn_id $host_spawn_id

    set timeout 5
    incr case_num
    send "reset_board 4\r"
    while 1 {
        expect {
            -re "powering ON board attached to printer port pin 4" {
                case_pass $case_num
                break
            }
            timeout {
                case_fail $case_num
            }
        }
    }

    set spawn_id $kermit_spawn_id

    set timeout 5
    incr case_num
    while 1 {
        expect {
            -re "$wakeup_pattern.*$kernel_prompt" {
                case_pass $case_num
                break
            }
            timeout {
                send_user "Test case 1: Wake up fail\n"
                incr wakeup_fail
                break
            }
        }
    }

    sleep 5
    incr case_num
    set timeout 15
    send -s "arecord -t wav -c 2 -f S16_LE -r 48000 -d 5 |aplay\n"
    while 1 {
        expect {
            -re "TUV|UVF|TU|UV|VF|dma" {
                incr count_under_run
                expect {
                    -re $kernel_prompt {
                        case_pass $case_num
                        break
                    }
                    timeout {
                        case_fail $case_num
                    }
                }
            }
            -re "(Input/output error|Hardware Trace:).*$kernel_prompt" {
                case_fail $case_num
            }
            -re "Stereo\r\n$kernel_prompt" {
                case_pass $case_num
                break
            }
            timeout {
                case_fail $case_num
            }
        }
    }
}

proc pm_test_case2_reset {} {
    global case_num
    global kernel_prompt
    global wakeup_fail
    global pm_state
    global sleep_pattern
    global wakeup_pattern
    global host_spawn_id
    global kermit_spawn_id
    global spawn_id

    set timeout 5
    incr case_num
    send "echo $pm_state > /sys/power/state\r"
    while 1 {
        expect {
            -re $sleep_pattern {
                case_pass $case_num
                break
            }
            timeout {
                send_user "Test case 2: Sleep fail\n"
                case_fail $case_num
            }
        }
    }

    sleep 10

    set spawn_id $host_spawn_id 

    set timeout 5
    incr case_num
    send "reset_board 4\r"
    while 1 {
        expect {
            -re "powering ON board attached to printer port pin 4" {
                case_pass $case_num
                break
            }
            timeout {
                case_fail $case_num
            }
        }
    }

    set spawn_id $kermit_spawn_id
    set timeout 5
    incr case_num
    while 1 {
        expect {
            -re $wakeup_pattern {
                case_pass $case_num
                break
            }
            timeout {
                send_user "Test case 2: Wake up fail\n"
                incr wakeup_fail
                break
            }
        }
    }

}

proc pm_test_case1_rtcwake {} {
    global case_num
    global kernel_prompt
    global sleep_time
    global wakeup_fail
    global pm_state
    global sleep_pattern
    global wakeup_pattern
    global count_under_run

    set timeout [expr $sleep_time + 10]
    incr case_num
    send "\nrtcwake -s $sleep_time -m $pm_state\r"
    while 1 {
        expect {
            -re "$sleep_pattern.*$wakeup_pattern" {
                case_pass $case_num
                break
            }
            timeout {
                send_user "Test case 1: Wake up fail\n"
                incr wakeup_fail
                break
            }
        }
    }

    set timeout 5
    expect -re "Full\r\n|lpa 0x41E1\r\n"

    incr case_num
    set timeout 10
    send -s "arecord -t wav -c 2 -f S32_LE -r 44100 -d 5 |aplay\n"
    while 1 {
        expect {
            -re "\[fF]ail|\[eE]rror" {
                case_fail $case_num
            }
            -re "Stereo\r\n$kernel_prompt" {
                case_pass $case_num
                break
            }
            timeout {
                case_fail $case_num
            }
        }
    }
}

proc pm_test_case2_rtcwake {} {
    global case_num
    global kernel_prompt
    global sleep_time
    global wakeup_fail
    global pm_state
    global sleep_pattern
    global wakeup_pattern
    global count_under_run

    set timeout [expr $sleep_time + 10]
    incr case_num
    send "\nrtcwake -s $sleep_time -m $pm_state\r"
    while 1 {
        expect {
            -re "$sleep_pattern.*$wakeup_pattern" {
                case_pass $case_num
                break
            }
            timeout {
                send_user "Test case 2: Wake up fail\n"
                incr wakeup_fail
                break
            }
        }
    }

    set timeout 5
    expect -re "Full\r\n|lpa 0x41E1\r\n"
}


step "Start testing"
set case_num 0
set Total_Count 5
set count_under_run 0

set timeout 15
expect -re "Up.*Full"
send "\r"
expect -re $kernel_prompt

if { $audio_dev == "audio_ssm2603" } {
    incr case_num
    set timeout 5
    send "amixer cget name='Capture Switch'\r"
    while 1 {
        expect {
            -re "values=off.*$kernel_prompt" {
                send "amixer cset name='Capture Switch' on\r"
                continue
            }
            -re "values=on.*$kernel_prompt" {
                case_pass $case_num
                break
            }
            timeout {
                case_fail $case_num
            }
        }
    }
}

if { $audio_dev == "audio_adau1361" } {
    incr case_num
    set timeout 15
    send "amixer cset name='Headphone Playback Switch' on; amixer cset name='Headphone Playback Volume' 40; amixer cset name='Left Playback Mixer Left DAC Switch' on; amixer cset name='Right Playback Mixer Left DAC Switch' on; amixer cset name='Lineout Playback Switch' on; amixer cset name='Lineout Playback Volume' 40; amixer cset name='Left LR Playback Mixer Left Volume' 2; amixer cset name='Right LR Playback Mixer Right Volume' 2; amixer cset name='Aux Capture Volume' 7; amixer cset name='Digital Capture Volume' 255; amixer cset name='Input 1 Capture Volume' 7; amixer cset name='Input 2 Capture Volume' 7; amixer cset name='Input 3 Capture Volume' 7; amixer cset name='Input 4 Capture Volume' 7\r"
    while 1 {
        expect {
            -re $kernel_prompt {
                case_pass $case_num
                break
            }
            timeout {
                case_fail $case_num
            }
        }
    }
}

if { $audio_dev == "audio_adau1761" } {
    incr case_num
    set timeout 15
    send "amixer cset name='Headphone Playback Switch' on; amixer cset name='Headphone Playback Volume' 40; amixer cset name='Right Playback Mixer Right DAC Switch' on; amixer cset name='Left Playback Mixer Left DAC Switch' on; amixer cset name='DSP Bypass Playback Switch' on; amixer cset name='DSP Bypass Capture Switch' on; amixer cset name='Digital Capture Volume' 255; amixer cset name='Input 2 Capture Volume' 7; amixer cset name='Input 4 Capture Volume' 7;\r"
    while 1 {
        expect {
            -re $kernel_prompt {
                case_pass $case_num
                break
            }
            timeout {
                case_fail $case_num
            }
        }
    }
}

incr case_num
set timeout 15
send -s "arecord -t wav -c 2 -f S16_LE -r 48000 -d 5 |aplay\n"
while 1 {
    expect {
        -re "TUV|UVF|TU|UV|VF|dma" {
            incr count_under_run
            expect {
                -re $kernel_prompt {
                    case_pass $case_num
                    break
                }
                timeout {
                    case_fail $case_num
                }
            }
        }
        -re "(Input/output error|Hardware Trace:).*$kernel_prompt" {
            case_fail $case_num
        }
        -re "Stereo\r\n$kernel_prompt" {
            case_pass $case_num
            break
        }
        timeout {
            case_fail $case_num
        }
    }
}

set timeout 5
incr case_num
send "cat /proc/mounts\r"
while 1 {
    expect {
        -re "sysfs /sys sysfs rw,nosuid,nodev,noexec.*$kernel_prompt" {
            case_pass $case_num
            break
        }
        timeout {
            case_fail $case_num
        }
    }
}

set timeout 5
incr case_num
send "ls /sys/power\r"
while 1 {
    expect {
        -re "state.*$kernel_prompt" {
            case_pass $case_num
            break
        }
        timeout {
            case_fail $case_num
        }
    }
}


spawn /bin/bash

set host_spawn_id $spawn_id
set timeout 5
expect -re $sh_prompt

incr case_num
set timeout 15
send "su\r"
while 1 {
    expect {
        -re "\[pP]assword" {
            send "$password\r"
            continue
        }
        -re "\[eE]rror|\[fF]ail" {
            case_fail $case_num
        }
        -re $sh_prompt {
            case_pass $case_num
            break
        }
        timeout {
            case_fail $case_num
        }
    }
}


set spawn_id $kermit_spawn_id
set timeout 5
expect -re $kernel_prompt


for {set count 1} {$count <= $Total_Count} {incr count} {
    if { $board_type == "BF609-EZKIT" } {
        pm_test_case1_reset
    } else {
        pm_test_case1_rtcwake
    }
    send_user "\nPM test case 1 round $count pass\n\n"
}

set timeout 15
incr case_num
send "tone &\n"
while 1 {
    expect {
        -re " \[0-9]+ tone\r\n$kernel_prompt" {
            case_pass $case_num
            break
        }
        timeout {
            case_fail $case_num
        }
    }
}

sleep 5
for {set count 1} {$count <= $Total_Count} {incr count} {
    if { $board_type == "BF609-EZKIT" } {
        pm_test_case2_reset
    } else {
        pm_test_case2_rtcwake
    }
    send_user "\nPM test case 2 round $count pass\n\n"
}

set timeout 20
incr case_num
send "audio_test_id=\`ps|awk \'\$5==\"tone\" {print \$1}\'\`; kill \$audio_test_id\r"
while 1 {
    expect {
        -re "(kill:|Usage:|\[eE]rror|\[fF]ail).*$kernel_prompt" {
            case_fail $case_num
        }
        -re $kernel_prompt {
            case_pass $case_num
            break
        }
        timeout {
            case_fail $case_num
        }
    }
}

send "\r"
expect -nocase -re "Done.*$kernel_prompt"

incr case_num
if { $wakeup_fail == 0 } {
    send_user "Wake up pass\n"
    case_pass $case_num
} else {
    send_user "Wake up fail $wakeup_fail times\n"
    case_fail $case_num
}

incr case_num
if { $count_under_run == 0 } {
    send_user "No under run error\n"
    case_pass $case_num
} else {
    send_user "Under run error occurs $count_under_run times\n"
    case_fail $case_num
}

all_pass
send_user "Ending $argv0\n"
log_file
